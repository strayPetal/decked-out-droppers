plugins {
    /* You can check and update plugin versions at:
    https://fabricmc.net/develop/
    https://github.com/Juuxel/loom-vineflower */

    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "Quilt"
        url = "https://maven.quiltmc.org/repository/release/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_mappings}:intermediary-v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
}

base {
    archivesName = project.archives_base_name
}

processResources {
    inputs.property "id", project.mod_id
    inputs.property "version", version
    inputs.property "name", project.mod_name
    inputs.property "description", project.mod_description
    inputs.property "author", project.mod_author

    filesMatching("fabric.mod.json") {
        def valueMap = [
                "id": project.mod_id,
                "version": version,
                "name": project.mod_name,
                "description": project.mod_description,
                "author": project.mod_author
        ]
        expand valueMap
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {}
}
